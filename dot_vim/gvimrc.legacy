"      ___                              ___         ___         ___
"     /  /\        ___      ___        /__/\       /  /\       /  /\
"    /  /:/_      /__/\    /  /\      |  |::\     /  /::\     /  /:/
"   /  /:/ /\     \  \:\  /  /:/      |  |:|:\   /  /:/\:\   /  /:/
"  /  /:/_/::\     \  \:\/__/::\    __|__|:|\:\ /  /:/~/:/  /  /:/  ___
" /__/:/__\/\:\___  \__\:\__\/\:\__/__/::::| \:/__/:/ /:/__/__/:/  /  /\
" \  \:\ /~~/:/__/\ |  |:|  \  \:\/\  \:\~~\__\\  \:\/:::::\  \:\ /  /:/
"  \  \:\  /:/\  \:\|  |:|   \__\::/\  \:\      \  \::/~~~~ \  \:\  /:/
"   \  \:\/:/  \  \:\__|:|   /__/:/  \  \:\      \  \:\      \  \:\/:/
"    \  \::/    \__\::::/    \__\/    \  \:\      \  \:\      \  \::/
"     \__\/         ~~~~               \__\/       \__\/       \__\/
"
" http://patorjk.com/software/taag/ - Isometric 3 Smush (U)
"

scriptencoding utf-8

" Change startup directory {{{1
if has('vim_starting') && has('win32') && getcwd() is? expand('$VIMRUNTIME')
  augroup gvimrc_startup
    autocmd!
    autocmd VimEnter *  cd ~
  augroup END
endif

" using the mouse {{{1
set mousehide

" メニュー {{{1
augroup gvimrc_menu
  autocmd!
augroup END

function! s:on_setting(value) abort "{{{
  return v:option_old !=# a:value && v:option_new =~# a:value
endfunction "}}}

function! s:load_menu() abort "{{{
  source $VIMRUNTIME/menu.vim
  set guioptions-=M
endfunction "}}}

" ツールバーまたはメニューの読み込み "{{{2
autocmd gvimrc_menu OptionSet guioptions
      \ if s:on_setting('T') || s:on_setting('m')  |
      \   call s:load_menu() |
      \ endif

" ポップアップメニュー {{{2
autocmd gvimrc_menu MenuPopup *
      \ call s:load_menu()

" DirectDraw {{{1
if has('directx') && empty(&renderoptions)
  augroup gvimrc_renderoptions
    autocmd!
    autocmd GUIEnter,ColorScheme *
          \ let &renderoptions = printf('type:directx,gamma:%1.1f,contrast:3,level:0.8,geom:1,renmode:4,taamode:2', &background is# 'light' ? 1.6 : 1.9)
  augroup END
endif

" GUI Options {{{1
set guioptions-=a guioptions+=A guioptions+=c guioptions-=m
set guioptions-=t guioptions-=T guioptions-=r guioptions-=L
set guioptions-=e
if has('patch-8.0.1609')
  set guioptions+=!
endif

" 1}}}
