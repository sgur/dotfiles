{{ if (and (lookPath "fzf") (lookPath "tv" | not)) -}}
status is-interactive || exit

if test -n "$TMUX"
    # https://raw.githubusercontent.com/tmux/tmux/3.2/CHANGES
    # tmux 3.2 以降で popup が利用可能
    if test (string match --regex '\d(?:\.\d+)?' (tmux -V)) -ge 3.2
        string match --regex -q '(?<major>\d)\.(?<minor>\d\d).(?<patch>\d)' -- (fzf --version)
        if test $major -gt 0 -o \( $major -eq 0 -a $minor -ge 53 \)
            set -gx FZF_CMD fzf --tmux -p 90%,60%
            set -gx FZF_TMUX_OPTS -p 90%,60%
        else
            set -gx FZF_CMD fzf-tmux -p 90%,60%
            set -gx FZF_TMUX_OPTS -p 90%,60%
        end
    else
        set -gx FZF_CMD fzf-tmux -d 60%
        set -gx FZF_TMUX_OPTS -d 60%
    end
    set -gx FZF_TMUX 1
else
    set -gx FZF_CMD fzf --height=60%
end

function fzf-grep
 set -l RELOAD reload:rg --column --color=always --smart-case {q} || :
 fzf --disabled --ansi \
     --bind "start:$RELOAD" --bind "change:$RELOAD" \
     --bind 'enter:become:$EDITOR {1} +{2}' \
     --bind 'ctrl-o:execute:$EDITOR {1} +{2}' \
     --delimiter : \
     --preview 'bat --style=full --color=always --highlight-line {2} {1}' \
     --preview-window '~4,+{2}+4/3,<80(up)'
end

# https://github.com/junegunn/fzf?tab=readme-ov-file#setting-up-shell-integration
# fzf --fish | FZF_CTRL_T_COMMAND= FZF_ALT_C_COMMAND= source
{{ output "fzf" "--fish" }}

function fzf-ghq-repository-widget
    set -l commandline (__fzf_parse_commandline)
    set -lx dir $commandline[1]
    set -l fzf_query $commandline[2]
    set -l prefix $commandline[3]

    test -n "$FZF_TMUX_HEIGHT"; or set FZF_TMUX_HEIGHT 40%
    begin
        set -lx FZF_DEFAULT_OPTS (__fzf_defaults "--reverse --scheme=path" "$FZF_GHQ_REPO_OPTS")

        set -lx FZF_DEFAULT_OPTS_FILE
        set -lx FZF_DEFAULT_COMMAND

        set FZF_DEFAULT_COMMAND 'ghq list'

        if set -l result (eval $FZF_DEFAULT_COMMAND \| (__fzfcmd) --no-multi --query=$fzf_query)
            cd -- (ghq list --exact --full-path $result)

            # Remove last token from commandline.
            commandline -t ""
            commandline -it -- $prefix
        end
    end

    commandline -f repaint
end

# https://zenn.dev/yamo/articles/5c90852c9c64ab
# fzf を使って git のブランチを選択して git switch する fish 関数
function fzf-git-branch-widget
    set -l commandline (__fzf_parse_commandline)
    set -lx dir $commandline[1]
    set -l fzf_query $commandline[2]
    set -l prefix $commandline[3]

    test -n "$FZF_TMUX_HEIGHT"; or set FZF_TMUX_HEIGHT 40%
    begin
        set -lx FZF_DEFAULT_OPTS (__fzf_defaults \
            '--bind=\'shift-delete:execute-silent(git branch --delete --force {})+reload(eval $FZF_DEFAULT_COMMAND)\'' \
            "--reverse" "$FZF_GIT_BRANCH_OPTS")

        set -lx FZF_DEFAULT_OPTS_FILE
        set -lx FZF_DEFAULT_COMMAND

        set FZF_DEFAULT_COMMAND \
            'git branch --all --format=\'%(refname:short)\' 2>/dev/null' \
            '| grep -v -e \'^origin$\''

        if set -l result (eval $FZF_DEFAULT_COMMAND \| (__fzfcmd) --no-multi --query=$fzf_query)
            git switch --quiet (string replace --regex '^origin/' '' $result)

            # Remove last token from commandline.
            commandline -t ""
            commandline -it -- $prefix
        end
    end

    commandline -f repaint
end

function fzf-zoxide-widget
    set -l commandline (__fzf_parse_commandline)
    set -lx dir $commandline[1]
    set -l fzf_query $commandline[2]
    set -l prefix $commandline[3]

    test -n "$FZF_TMUX_HEIGHT"; or set FZF_TMUX_HEIGHT 40%
    begin
        set -lx FZF_DEFAULT_OPTS (__fzf_defaults "--reverse --scheme=path" "$FZF_ZOXIDE_OPTS")

        set -lx FZF_DEFAULT_OPTS_FILE
        set -lx FZF_DEFAULT_COMMAND

        set FZF_DEFAULT_COMMAND 'zoxide query --list --score'

        if set -l result (eval $FZF_DEFAULT_COMMAND \| (__fzfcmd) --no-multi --no-sort --query=$fzf_query \
            | tr -s ' ' \
            | cut -d ' ' -f 3-)
            cd -- $result

            # Remove last token from commandline.
            commandline -t ""
            commandline -it -- $prefix
        end
    end

    commandline -f repaint
end

function fzf-aws-profile-widget
    set -l commandline (__fzf_parse_commandline)
    set -lx dir $commandline[1]
    set -l fzf_query $commandline[2]
    set -l prefix $commandline[3]

    test -n "$FZF_TMUX_HEIGHT"; or set FZF_TMUX_HEIGHT 40%
    begin
        set -lx FZF_DEFAULT_OPTS (__fzf_defaults "--reverse" "$FZF_AWS_PROFILE_OPTS")

        set -lx FZF_DEFAULT_OPTS_FILE
        set -lx FZF_DEFAULT_COMMAND

        set FZF_DEFAULT_COMMAND "aws-vault list --profiles"

        if set -l result (eval $FZF_DEFAULT_COMMAND \| (__fzfcmd) --no-multi --query=$fzf_query)
            set -l action exec login export
            string join \n $action | eval (__fzfcmd) --no-multi | read -l method

            # Remove last token from commandline.
            commandline -t ""

            switch $method
                case exec
                    commandline -it -- "aws-vault exec $result"
                case login
                    aws-vault login $result
                case export
                    commandline -it -- "env (aws-vault export $result)"
                case "*"
                    commandline -it -- $prefix
            end
        end
    end

    commandline -f repaint
end

function fzf-pass-totp-widget
    set -l commandline (__fzf_parse_commandline)
    set -lx dir $commandline[1]
    set -l fzf_query $commandline[2]
    set -l prefix $commandline[3]

    test -n "$FZF_TMUX_HEIGHT"; or set FZF_TMUX_HEIGHT 40%
    begin
        set -lx FZF_DEFAULT_OPTS (__fzf_defaults "--reverse" "$FZF_PASS_TOTP_OPTS")

        set -lx FZF_DEFAULT_OPTS_FILE
        set -lx FZF_DEFAULT_COMMAND

        pushd ~/.password-store

        set FZF_DEFAULT_COMMAND 'find totp/ -type f -print0 | sed -z \'s/\.gpg$//\''

        if set -l result (eval $FZF_DEFAULT_COMMAND \| (__fzfcmd) --no-multi --read0 --query=$fzf_query)
            pass otp $result | fish_clipboard_copy

            # Remove last token from commandline.
            commandline -t ""
            commandline -it -- $prefix
        end
        popd
    end

    commandline -f repaint
end

if not set -q FZF_ALT_V_COMMAND; or test -n "$FZF_ALT_V_COMMAND"
    bind alt-v fzf-git-branch-widget
end
if not set -q FZF_ALT_X_COMMAND; or test -n "$FZF_ALT_X_COMMAND"
    bind alt-x fzf-ghq-repository-widget
end
if not set -q FZF_ALT_Z_COMMAND; or test -n "$FZF_ALT_Z_COMMAND"
    bind alt-z fzf-zoxide-widget
end
bind alt-a fzf-aws-profile-widget
bind alt-i fzf-pass-totp-widget

if bind -M insert &> /dev/null
    if not set -q FZF_ALT_V_COMMAND; or test -n "$FZF_ALT_V_COMMAND"
        bind -M insert alt-v fzf-git-branch-widget
    end
    if not set -q FZF_ALT_X_COMMAND; or test -n "$FZF_ALT_X_COMMAND"
        bind -M insert alt-x fzf-ghq-repository-widget
    end
    if not set -q FZF_ALT_Z_COMMAND; or test -n "$FZF_ALT_Z_COMMAND"
        bind -M insert alt-z fzf-zoxide-widget
    end
    bind -M insert alt-a fzf-aws-profile-widget
    bind -M insert alt-i fzf-pass-totp-widget
end

set FZF_CTRL_T_OPTS "--walker-skip .git,node_modules,target
--preview 'bat -n --color=always {}'
--bind 'ctrl-/:change-preview-window(down|hidden|)'"

set FZF_CTRL_R_OPTS "--prompt='history> '
--query='$argv'
--preview 'echo {}'
--preview-window up:3:hidden:wrap
--bind 'ctrl-/:toggle-preview'"

set FZF_ALT_C_OPTS "--walker-skip .git,node_modules,target
--preview 'command tree -C {}'
--bind 'ctrl-/:toggle-preview'"

set FZF_GHQ_REPO_OPTS "--prompt='repository> '
--preview 'git -C (ghq list --exact --full-path {}) log -5 --graph --decorate --abbrev-commit --color=always'
--bind 'ctrl-/:toggle-preview'"

set FZF_ZOXIDE_OPTS "--prompt='zoxide> '
--preview 'command tree -C (echo {} | tr -s \" \" | cut -d \" \" -f 3-)'
--bind 'ctrl-/:toggle-preview'"

set FZF_GIT_BRANCH_OPTS "--prompt='branch> '
--preview 'git log -5 --graph --decorate --abbrev-commit --color=always {}'
--bind 'ctrl-/:toggle-preview'"

set FZF_AWS_PROFILE_OPTS "--prompt='AWS profile> '"

set FZF_PASS_TOTP_OPTS "--prompt='pass totp> '"

# catppuccin/latte
# set -gx FZF_DEFAULT_OPTS $FZF_DEFAULT_OPTS "\
# --color=bg+:#ccd0da,bg:#eff1f5,spinner:#dc8a78,hl:#d20f39 \
# --color=fg:#4c4f69,header:#d20f39,info:#8839ef,pointer:#dc8a78 \
# --color=marker:#dc8a78,fg+:#4c4f69,prompt:#8839ef,hl+:#d20f39"

# catppuccin/frappe
# set -gx FZF_DEFAULT_OPTS $FZF_DEFAULT_OPTS "\
# --color=bg+:#414559,bg:#303446,spinner:#f2d5cf,hl:#e78284 \
# --color=fg:#c6d0f5,header:#e78284,info:#ca9ee6,pointer:#f2d5cf \
# --color=marker:#f2d5cf,fg+:#c6d0f5,prompt:#ca9ee6,hl+:#e78284"

# catppuccin/macchiato
# set -gx FZF_DEFAULT_OPTS $FZF_DEFAULT_OPTS "\
# --color=bg+:#363a4f,bg:#24273a,spinner:#f4dbd6,hl:#ed8796 \
# --color=fg:#cad3f5,header:#ed8796,info:#c6a0f6,pointer:#f4dbd6 \
# --color=marker:#f4dbd6,fg+:#cad3f5,prompt:#c6a0f6,hl+:#ed8796"

# catppuccin/mocha
# set -gx FZF_DEFAULT_OPTS $FZF_DEFAULT_OPTS "\
# --color=bg+:#313244,bg:#1e1e2e,spinner:#f5e0dc,hl:#f38ba8 \
# --color=fg:#cdd6f4,header:#f38ba8,info:#cba6f7,pointer:#f5e0dc \
# --color=marker:#f5e0dc,fg+:#cdd6f4,prompt:#cba6f7,hl+:#f38ba8"
{{- end }}
