{{ if lookPath "fzf" -}}
status is-interactive || exit

if test -n "$TMUX"
    # https://raw.githubusercontent.com/tmux/tmux/3.2/CHANGES
    # tmux 3.2 以降で popup が利用可能
    if test (string match --regex '\d(?:\.\d+)?' (tmux -V)) -ge 3.2
        string match --regex -q '(?<major>\d)\.(?<minor>\d\d).(?<patch>\d)' -- (fzf --version)
        if test $major -gt 0 -o \( $major -eq 0 -a $minor -ge 53 \)
            set -gx FZF_CMD fzf --tmux -p 90%,60%
            set -gx FZF_TMUX_OPTS -p 90%,60%
        else
            set -gx FZF_CMD fzf-tmux -p 90%,60%
            set -gx FZF_TMUX_OPTS -p 90%,60%
        end
    else
        set -gx FZF_CMD fzf-tmux -d 60%
        set -gx FZF_TMUX_OPTS -d 60%
    end
    set -gx FZF_TMUX 1
else
    set -gx FZF_CMD fzf --height=60%
end

function fzf-grep
 set -l RELOAD reload:rg --column --color=always --smart-case {q} || :
 fzf --disabled --ansi \
     --bind "start:$RELOAD" --bind "change:$RELOAD" \
     --bind 'enter:become:$EDITOR {1} +{2}' \
     --bind 'ctrl-o:execute:$EDITOR {1} +{2}' \
     --delimiter : \
     --preview 'bat --style=full --color=always --highlight-line {2} {1}' \
     --preview-window '~4,+{2}+4/3,<80(up)'
end

# https://github.com/junegunn/fzf?tab=readme-ov-file#setting-up-shell-integration
# fzf --fish | FZF_CTRL_T_COMMAND= FZF_ALT_C_COMMAND= source
{{ output "fzf" "--fish" }}

function fzf-ghq-repository-widget
    set -l commandline (__fzf_parse_commandline)
    set -lx dir $commandline[1]
    set -l fzf_query $commandline[2]
    set -l prefix $commandline[3]

    test -n "$FZF_TMUX_HEIGHT"; or set FZF_TMUX_HEIGHT 40%
    begin
        set -lx FZF_DEFAULT_OPTS (__fzf_defaults "--reverse --scheme=path" "$FZF_ALT_X_OPTS")
        set -lx FZF_DEFAULT_OPTS_FILE ''
        set -lx FZF_DEFAULT_COMMAND "$FZF_ALT_X_COMMAND"
        ghq list | eval (__fzfcmd) --no-multi --query=$fzf_query | read -l result

        if test -n "$result"
            cd -- (ghq list --exact --full-path $result)

            # Remove last token from commandline.
            commandline -t ""
            commandline -it -- $prefix
        end
    end

    commandline -f repaint
end

# https://zenn.dev/yamo/articles/5c90852c9c64ab
# fzf を使って git のブランチを選択して git switch する fish 関数
function fzf-git-branch-widget
    set -l commandline (__fzf_parse_commandline)
    set -lx dir $commandline[1]
    set -l fzf_query $commandline[2]
    set -l prefix $commandline[3]

    test -n "$FZF_TMUX_HEIGHT"; or set FZF_TMUX_HEIGHT 40%
    begin
        set -lx FZF_DEFAULT_OPTS (__fzf_defaults "--reverse" "$FZF_ALT_A_OPTS")
        set -lx FZF_DEFAULT_OPTS_FILE ''
        set -lx FZF_DEFAULT_COMMAND "$FZF_ALT_A_COMMAND"

        git branch --all --format='%(refname:short)' 2> /dev/null \
            | grep -v -e '^origin$' \
            | eval (__fzfcmd) --no-multi --query=$fzf_query | read -l result

        if test -n "$result"
            git switch --quiet (string replace --regex '^origin/' '' $result)

            # Remove last token from commandline.
            commandline -t ""
            commandline -it -- $prefix
        end
    end

    commandline -f repaint
end

function fzf-zoxide-widget
    set -l commandline (__fzf_parse_commandline)
    set -lx dir $commandline[1]
    set -l fzf_query $commandline[2]
    set -l prefix $commandline[3]

    test -n "$FZF_TMUX_HEIGHT"; or set FZF_TMUX_HEIGHT 40%
    begin
        set -lx FZF_DEFAULT_OPTS (__fzf_defaults "--reverse --scheme=path" "$FZF_ALT_Z_OPTS")
        set -lx FZF_DEFAULT_OPTS_FILE ''
        set -lx FZF_DEFAULT_COMMAND "$FZF_ALT_Z_COMMAND"
        zoxide query --list --score | eval (__fzfcmd) --no-multi --no-sort --query=$fzf_query | tr -s ' ' | cut -d ' ' -f 3- | read -l result

        if test -n "$result"
            cd -- $result

            # Remove last token from commandline.
            commandline -t ""
            commandline -it -- $prefix
        end
    end

    commandline -f repaint
end

function fzf-aws-profile-widget
    set -l commandline (__fzf_parse_commandline)
    set -lx dir $commandline[1]
    set -l fzf_query $commandline[2]
    set -l prefix $commandline[3]

    test -n "$FZF_TMUX_HEIGHT"; or set FZF_TMUX_HEIGHT 40%
    begin
        set -lx FZF_DEFAULT_OPTS (__fzf_defaults "--reverse")
        set -lx FZF_DEFAULT_OPTS_FILE ''
        set -lx FZF_DEFAULT_COMMAND ""
        aws-vault list --profiles | eval (__fzfcmd) --no-multi --query=$fzf_query | read -l result

        if test -n "$result"
            if test 0 -eq (count $argv)
                echo -e exec\nlogin\nexport\nrotate | eval (__fzfcmd) --no-multi | read -l method
                aws-vault $method $result
            else
                aws-vault $argv[1] $result
            end

            # Remove last token from commandline.
            commandline -t ""
            commandline -it -- $prefix
        end
    end

    commandline -f repaint
end

if not set -q FZF_ALT_A_COMMAND; or test -n "$FZF_ALT_A_COMMAND"
    bind alt-a fzf-git-branch-widget
    bind alt-\; fzf-git-branch-widget
end
if not set -q FZF_ALT_X_COMMAND; or test -n "$FZF_ALT_X_COMMAND"
    bind alt-x fzf-ghq-repository-widget
end
if not set -q FZF_ALT_Z_COMMAND; or test -n "$FZF_ALT_Z_COMMAND"
    bind alt-z fzf-zoxide-widget
end
bind alt-g fzf-aws-profile-widget

if bind -M insert &> /dev/null
    if not set -q FZF_ALT_A_COMMAND; or test -n "$FZF_ALT_A_COMMAND"
        bind -M insert alt-a fzf-git-branch-widget
        bind -M insert alt-\; fzf-git-branch-widget
    end
    if not set -q FZF_ALT_X_COMMAND; or test -n "$FZF_ALT_X_COMMAND"
        bind -M insert alt-x fzf-ghq-repository-widget
    end
    if not set -q FZF_ALT_Z_COMMAND; or test -n "$FZF_ALT_Z_COMMAND"
        bind -M insert alt-z fzf-zoxide-widget
    end
    bind alt-g fzf-aws-profile-widget
end


set FZF_ALT_Z_OPTS "--prompt='zoxide> '
--preview 'command tree -C {}'
--bind 'ctrl-/:toggle-preview'"

set FZF_CTRL_T_OPTS "--walker-skip .git,node_modules,target
--preview 'bat -n --color=always {}'
--bind 'ctrl-/:change-preview-window(down|hidden|)'"

set FZF_CTRL_R_OPTS "--prompt='history> '
--query='$argv'
--preview 'echo {}'
--preview-window up:3:hidden:wrap
--bind 'ctrl-/:toggle-preview'"

set FZF_ALT_C_OPTS "--walker-skip .git,node_modules,target
--preview 'command tree -C {}'
--bind 'ctrl-/:toggle-preview'"

set FZF_ALT_X_OPTS "--prompt='repository> '
--preview='git -C (ghq list --exact --full-path {}) log -5 --graph --decorate --abbrev-commit --color=always'
--bind 'ctrl-/:toggle-preview'"

set FZF_ALT_A_OPTS "--prompt='branch> '
--preview 'git log -5 --graph --decorate --abbrev-commit --color=always {}'
--bind 'ctrl-/:toggle-preview'"


# catppuccin/latte
# set -gx FZF_DEFAULT_OPTS $FZF_DEFAULT_OPTS "\
# --color=bg+:#ccd0da,bg:#eff1f5,spinner:#dc8a78,hl:#d20f39 \
# --color=fg:#4c4f69,header:#d20f39,info:#8839ef,pointer:#dc8a78 \
# --color=marker:#dc8a78,fg+:#4c4f69,prompt:#8839ef,hl+:#d20f39"

# catppuccin/frappe
# set -gx FZF_DEFAULT_OPTS $FZF_DEFAULT_OPTS "\
# --color=bg+:#414559,bg:#303446,spinner:#f2d5cf,hl:#e78284 \
# --color=fg:#c6d0f5,header:#e78284,info:#ca9ee6,pointer:#f2d5cf \
# --color=marker:#f2d5cf,fg+:#c6d0f5,prompt:#ca9ee6,hl+:#e78284"

# catppuccin/macchiato
# set -gx FZF_DEFAULT_OPTS $FZF_DEFAULT_OPTS "\
# --color=bg+:#363a4f,bg:#24273a,spinner:#f4dbd6,hl:#ed8796 \
# --color=fg:#cad3f5,header:#ed8796,info:#c6a0f6,pointer:#f4dbd6 \
# --color=marker:#f4dbd6,fg+:#cad3f5,prompt:#c6a0f6,hl+:#ed8796"

# catppuccin/mocha
# set -gx FZF_DEFAULT_OPTS $FZF_DEFAULT_OPTS "\
# --color=bg+:#313244,bg:#1e1e2e,spinner:#f5e0dc,hl:#f38ba8 \
# --color=fg:#cdd6f4,header:#f38ba8,info:#cba6f7,pointer:#f5e0dc \
# --color=marker:#f5e0dc,fg+:#cdd6f4,prompt:#cba6f7,hl+:#f38ba8"
{{- end }}
