{{ if and (lookPath "gpg") (lookPath "gpgconf") -}}
if type -q gpg
    set -gx GPG_TTY (tty)
    gpg-connect-agent --quiet updatestartuptty /bye >/dev/null &
    set -gx SSH_AUTH_SOCK (gpgconf --list-dirs agent-ssh-socket)
    commandline -f repaint
end
{{- end }}

# meaningful-ooo/sponge
set sponge_purge_only_on_exit true
set sponge_delay 5

fish_config theme choose 'Catppuccin Mocha'

if status is-interactive
    if test -z "$EDITOR"
    {{- if lookPath "hx"}}
        set -gx EDITOR hx
    {{- else if lookPath "vim"}}
        set -gx EDITOR vim
    {{- end}}
    end

    set -l os (uname -s)
    # Linux
    # Darwin
    set -l arch (uname -m)
    # x86_64
    # arm64

    if test $os = Darwin
        function rm --wraps rm --description 'rm with -i option by default'
            command rm -i $argv
        end
    else
        function rm --wraps rm --description 'rm with --interactive option by default'
            command rm --interactive=once $argv
        end
    end
    function cp --wraps=cp --description 'cp with -i option by default'
        command cp -i $argv
    end
    function mv --wraps=mv --description 'mv with -i option by default'
        command mv -i $argv
    end

    # gazorby/fish-abbreviation-tip が conf.d のロード中に初期化されるので
    # config.fish 内で abbr を設定する
    abbr --add ipecho -- curl --silent https://ipecho.net/plain
    abbr --add checkip -- curl --silent https://checkip.amazonaws.com/

    if type -q tmux
        abbr --add tm tmux new-session -A
    end

    if type -q zellij
        abbr --add za zellij attach
        abbr --add za:c -- zellij attach --create
        abbr --add zl zellij list-sessions
    end

    if type -q chezmoi
        abbr --add chz -- chezmoi
        abbr --add chz:cd -- cd (chezmoi source-path)
    end

    abbr --add CC --position anywhere --set-cursor "%| fish_clipboard_copy"
    abbr --add ^^ --position anywhere -- "(git rev-parse --show-toplevel)"

    abbr --add hostname uname -n

    if type -q _na
        abbr --add n --function _na
    end

    abbr --add jira-create-subtask --set-cursor "jira issue create --type Sub-task --parent % --summary --body --no-edit"
    abbr --add jira-batch-create-subtask --set-cursor "cat | xargs -i -- jira issue create -t Sub-task -P % --no-input -s {} -b \"\""

    if type -q aws
        for profile in (rg --no-line-number '\[profile' ~/.aws/config | sed 's/\[profile \(.\+\)\]/\1/')
            abbr --add $profile:exec aws-vault exec $profile
            abbr --add $profile:login --set-cursor "aws-vault login $profile %--stdout | fish_clipboard_copy"
        end
    end

    if type -q pass && type -q fzf
        function __fzf_totp
            find ~/.password-store/totp/ -type f -print0 | sed -z 's/^.\+\/totp//' | sed -z 's/\.gpg$//' | fzf --read0 | sed 's/^/totp/' | read pass
            echo "otp $pass"
        end
        abbr --add otp --command pass --function __fzf_totp
    end

    # \el -> \e, or \co へ移設
    bind --user alt-comma __fish_list_current_token
    bind --user ctrl-o __fish_list_current_token
    bind --user alt-g 'gh auth switch' fish_prompt
end

emit fish_ready

# 共通の設定はログインシェルでのみ行う
status is-login || exit

{{ $homeDir := .chezmoi.homeDir -}}
{{ range (list ".yarn/bin" ".cargo/bin" ".dotnet/tools") -}}
{{   $path := joinPath $homeDir . -}}
{{   if get (stat $path) "isDir" -}}
fish_add_path -g {{ $path }}
{{   end -}}
{{ end -}}
fish_add_path -g ~/bin ~/.local/bin

if not contains /usr/share/fish/vendor_completions.d $fish_complete_path
    set -g fish_complete_path $fish_complete_path /usr/share/fish/vendor_completions.d
end
if not contains /usr/share/fish/completions $fish_complete_path
    set -g fish_complete_path $fish_complete_path /usr/share/fish/completions
end
if not contains ~/.local/share/fish/generated_completions $fish_complete_path
    set -g fish_complete_path $fish_complete_path ~/.local/share/fish/generated_completions
end

{{- if .wsl }}
set -g fish_help_browser (which xdg-open)
{{- end }}

set -gx LANG ja_JP.UTF-8
set -gx PIPENV_VENV_IN_PROJECT 1
set -gx RIPGREP_CONFIG_PATH ~/.config/ripgrep/ripgreprc
set -gx HGREP_DEFAULT_OPTS --theme Nord
set -gx LESS --incsearch --quit-if-one-screen --raw-control-chars
{{- if lookPath "bat" }}
set -gx PAGER bat
{{- end }}

{{ if eq .chezmoi.os "darwin" -}}
if test "$TERM" = "screen-256color"
    set -gx TERM xterm-256color
end
{{- end }}

